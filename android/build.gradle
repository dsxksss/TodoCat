buildscript {
    ext.kotlin_version = '2.1.0'
    repositories {
        maven { url 'https://maven.aliyun.com/repository/google' }
        maven { url 'https://maven.aliyun.com/repository/public' }
        maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        maven { url 'https://maven.aliyun.com/repository/google' }
        maven { url 'https://maven.aliyun.com/repository/public' }
        maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }
        google()
        mavenCentral()
    }

    configurations.all {
        resolutionStrategy {
            force 'androidx.core:core:1.9.0'
            force 'androidx.core:core-ktx:1.9.0'
            force 'androidx.appcompat:appcompat:1.6.1'
        }
    }
}

subprojects {
    afterEvaluate {project ->
        if (project.hasProperty('android')) {
            android {
                compileSdkVersion 35
                
                // 为缺少 namespace 的插件设置 namespace
                if (project.name == 'flutter_statusbarcolor_ns') {
                    namespace = 'com.sameer.flutterstatusbarcolor.flutterstatusbarcolor'
                } else {
                    // 尝试从 AndroidManifest.xml 读取包名作为 namespace
                    def manifestFile = project.file('src/main/AndroidManifest.xml')
                    if (manifestFile.exists()) {
                        def manifestContent = manifestFile.text
                        def packageMatch = manifestContent =~ /package="([^"]+)"/
                        if (packageMatch) {
                            namespace = packageMatch[0][1]
                        }
                    }
                }
                
                defaultConfig {
                    targetSdkVersion 35
                    minSdkVersion 23
                }

                lintOptions {
                    checkReleaseBuilds false
                    disable 'InvalidPackage'
                }
            }
        }
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
    project.evaluationDependsOn(':app')
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}